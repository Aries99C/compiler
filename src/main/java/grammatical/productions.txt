translation_unit::=external_decl
translation_unit::=translation_unit external_decl
external_decl::=function_definition
external_decl::=decl
function_definition::=decl_specs declarator decl_list compound_stat
function_definition::=declarator decl_list compound_stat
function_definition::=decl_specs declarator	compound_stat
function_definition::=declarator compound_stat
decl::=decl_specs init_declarator_list SEMI
decl::=decl_specs SEMI
decl_list::=decl
decl_list::=decl_list decl
decl_specs::=storage_class_spec decl_specs
decl_specs::=storage_class_spec
decl_specs::=type_spec decl_specs
decl_specs::=type_spec
decl_specs::=type_qualifier decl_specs
decl_specs::=type_qualifier
storage_class_spec::=AUTO
storage_class_spec::=REGISTER
storage_class_spec::=STATIC
storage_class_spec::=EXTERN
storage_class_spec::=TYPEDEF
type_spec::=VOID
type_spec::=CHAR
type_spec::=SHORT
type_spec::=INT
type_spec::=LONG
type_spec::=FLOAT
type_spec::=DOUBLE
type_spec::=SIGNED
type_spec::=UNSIGNED
type_spec::=struct_or_union_spec
type_spec::=enum_spec
type_qualifier::=CONST
type_qualifier::=VOLATILE
struct_or_union_spec::=struct_or_union IDN LP struct_decl_list RP
struct_or_union_spec::=struct_or_union LP struct_decl_list RP
struct_or_union_spec::=struct_or_union IDN
struct_or_union::=STRUCT
struct_or_union::=UNION
struct_decl_list::=struct_decl
struct_decl_list::=struct_decl_list struct_decl
init_declarator_list::=init_declarator
init_declarator_list::=init_declarator_list COMA init_declarator
init_declarator::=declarator
init_declarator::=declarator EQS initializer
struct_decl::=spec_qualifier_list struct_declarator_list SEMI
spec_qualifier_list::=type_spec spec_qualifier_list
spec_qualifier_list::=type_spec
spec_qualifier_list::=type_qualifier spec_qualifier_list
spec_qualifier_list::=type_qualifier
struct_declarator_list::=struct_declarator
struct_declarator_list::=struct_declarator_list COMA struct_declarator
struct_declarator::=declarator
struct_declarator::=declarator CLN const_exp
struct_declarator::=CLN const_exp
enum_spec::=ENUM IDN LP enumerator_list RP
enum_spec::=ENUM LP enumerator_list RP
enum_spec::=ENUM IDN
enumerator_list::=enumerator
enumerator_list::=enumerator_list COMA enumerator
enumerator::=IDN
enumerator::=IDN EQS const_exp
declarator::=pointer direct_declarator
declarator::=direct_declarator
direct_declarator::=IDN
direct_declarator::=SLP declarator SRP
direct_declarator::=direct_declarator MLP const_exp MRP
direct_declarator::=direct_declarator MLP MRP
direct_declarator::=direct_declarator SLP param_type_list SRP
direct_declarator::=direct_declarator SLP IDN_list SRP
direct_declarator::=direct_declarator SLP SRP
pointer::=TM type_qualifier_list
pointer::=TM
pointer::=TM type_qualifier_list pointer
pointer::=TM pointer
type_qualifier_list::=type_qualifier
type_qualifier_list::=type_qualifier_list type_qualifier
param_type_list::=param_list
param_type_list::=param_list COMA DOT DOT DOT
param_list::=param_decl
param_list::=param_list COMA param_decl
param_decl::=decl_specs declarator
param_decl::=decl_specs abstract_declarator
param_decl::=decl_specs
IDN_list::=IDN
IDN_list::=IDN_list COMA IDN
initializer::=assignment_exp
initializer::=LP initializer_list RP
initializer::=LP initializer_list COMA RP
initializer_list::=initializer
initializer_list::=initializer_list COMA initializer
type_name::=spec_qualifier_list abstract_declarator
type_name::=spec_qualifier_list
abstract_declarator::=pointer
abstract_declarator::=pointer direct_abstract_declarator
abstract_declarator::=direct_abstract_declarator
direct_abstract_declarator::=SLP abstract_declarator SRP
direct_abstract_declarator::=direct_abstract_declarator MLP const_exp MRP
direct_abstract_declarator::=MLP const_exp MRP
direct_abstract_declarator::=direct_abstract_declarator MLP	MRP
direct_abstract_declarator::=MLP MRP
direct_abstract_declarator::=direct_abstract_declarator SLP param_type_list SRP
direct_abstract_declarator::=SLP param_type_list SRP
direct_abstract_declarator::=direct_abstract_declarator SLP SRP
direct_abstract_declarator::=SLP SRP
stat::=labeled_stat
stat::=exp_stat
stat::=compound_stat
stat::=selection_stat
stat::=iteration_stat
stat::=jump_stat
labeled_stat::=IDN CLN stat
labeled_stat::=CASE const_exp CLN stat
labeled_stat::=DEFAULT CLN stat
exp_stat::=exp SEMI
exp_stat::=SEMI
compound_stat::=LP decl_list stat_list RP
compound_stat::=LP stat_list RP
compound_stat::=LP decl_list RP
compound_stat::=LP RP
stat_list::=stat
stat_list::=stat_list stat
selection_stat::=IF SLP exp SRP stat
selection_stat::=IF SLP exp SRP stat ELSE stat
selection_stat::=SWITCH SLP exp SRP stat
iteration_stat::=WHILE SLP exp SRP stat
iteration_stat::=DO stat WHILE SLP exp SRP SEMI
iteration_stat::=FOR SLP exp SEMI exp SEMI exp SRP stat
iteration_stat::=FOR SLP exp SEMI exp SEMI SRP stat
iteration_stat::=FOR SLP exp SEMI SEMI exp SRP stat
iteration_stat::=FOR SLP exp SEMI SEMI SRP stat
iteration_stat::=FOR SLP SEMI exp SEMI exp SRP stat
iteration_stat::=FOR SLP SEMI exp SEMI SRP stat
iteration_stat::=FOR SLP SEMI SEMI exp SRP stat
iteration_stat::=FOR SLP SEMI SEMI SRP stat
jump_stat::=GOTO IDN SEMI
jump_stat::=CONTINUE SEMI
jump_stat::=BREAK SEMI
jump_stat::=RETURN exp SEMI
jump_stat::=RETURN SEMI
exp::=assignment_exp
exp::=exp COMA assignment_exp
assignment_exp::=conditional_exp
assignment_exp::=unary_exp assignment_operator assignment_exp
assignment_operator::=EQS
assignment_operator::=TME
assignment_operator::=DVE
assignment_operator::=MDE
assignment_operator::=PLE
assignment_operator::=MSE
assignment_operator::=LSHE
assignment_operator::=RSHE
assignment_operator::=CAPE
assignment_operator::=XORE
assignment_operator::=CUPE
conditional_exp::=logical_or_exp
conditional_exp::=logical_or_exp QMK exp CLN conditional_exp
const_exp::=conditional_exp
logical_or_exp::=logical_and_exp
logical_or_exp::=logical_or_exp OR logical_and_exp
logical_and_exp::=inclusive_or_exp
logical_and_exp::=logical_and_exp AND inclusive_or_exp
inclusive_or_exp::=exclusive_or_exp
inclusive_or_exp::=inclusive_or_exp CUP exclusive_or_exp
exclusive_or_exp::=and_exp
exclusive_or_exp::=exclusive_or_exp XOR and_exp
and_exp::=equality_exp
and_exp::=and_exp CAP equality_exp
equality_exp::=relational_exp
equality_exp::=equality_exp EQ relational_exp
equality_exp::=equality_exp NE relational_exp
relational_exp::=shift_expression
relational_exp::=relational_exp LT shift_expression
relational_exp::=relational_exp GT shift_expression
relational_exp::=relational_exp LE shift_expression
relational_exp::=relational_exp GE shift_expression
shift_expression::=additive_exp
shift_expression::=shift_expression LSH additive_exp
shift_expression::=shift_expression RSH additive_exp
additive_exp::=mult_exp
additive_exp::=additive_exp PLUS mult_exp
additive_exp::=additive_exp MINUS mult_exp
mult_exp::=cast_exp
mult_exp::=mult_exp TM cast_exp
mult_exp::=mult_exp DIV cast_exp
mult_exp::=mult_exp MOD cast_exp
cast_exp::=unary_exp
cast_exp::=SLP type_name SRP cast_exp
unary_exp::=postfix_exp
unary_exp::=INC unary_exp
unary_exp::=DSC unary_exp
unary_exp::=unary_operator cast_exp
unary_exp::=SIZEOF unary_exp
unary_exp::=SIZEOF SLP type_name SRP
unary_operator::=CAP
unary_operator::=TM
unary_operator::=PLUS
unary_operator::=MINUS
unary_operator::=NGT
unary_operator::=NOT
postfix_exp::=primary_exp
postfix_exp::=postfix_exp MLP exp MRP
postfix_exp::=postfix_exp SLP argument_exp_list SRP
postfix_exp::=postfix_exp SLP SRP
postfix_exp::=postfix_exp DOT IDN
postfix_exp::=postfix_exp MINUS GT IDN
postfix_exp::=postfix_exp INC
postfix_exp::=postfix_exp DSC
primary_exp::=IDN
primary_exp::=const
primary_exp::=STR
primary_exp::=SLP exp SRP
argument_exp_list::=assignment_exp
argument_exp_list::=argument_exp_list COMA assignment_exp
const::=DINT
const::=CH
const::=FP
const::=ENM